!!ARBvp1.0

# Shader para el efecto halo
# Simplemente incrementa el tamaño del objeto un 30% según sus normales, y aplica un alpha al
# vértice según el ángulo entre el objeto y la cámara
#
# Requisitos y comentarios
#
# - Ninguno especial :)

# Constantes
PARAM mvp[4]     = { state.matrix.mvp };   #   modelview projection matrix
PARAM mvinv[4] = { state.matrix.modelview.invtrans };
PARAM tamano = program.local[0];

# Per-vertex inputs
ATTRIB inPosition = vertex.position;
ATTRIB inNormal = vertex.normal;
ATTRIB inColor    = vertex.color;

# Per-vertex outputs
OUTPUT outPosition    = result.position;
OUTPUT outColor    = result.color;

# Temporales

TEMP xfNormal, inPosTemp;


# Engordamos el objeto según su normal

MAD inPosTemp, inNormal, tamano, inPosition; # Sumamos un 30% al vértice según su normal
MOV inPosTemp.w, 1.0;

# Primero pasamos a coordenadas de vista

DP4 outPosition.x, mvp[0], inPosTemp;   # Transform the x component of the per-vertex position into clip-space
DP4 outPosition.y, mvp[1], inPosTemp;   # Transform the y component of the per-vertex position into clip-space
DP4 outPosition.z, mvp[2], inPosTemp;   # Transform the z component of the per-vertex position into clip-space
DP4 outPosition.w, mvp[3], inPosTemp;   # Transform the w component of the per-vertex position into clip-space

# Transformamos la normal y la normalizamos
DP3 xfNormal.x, mvinv[0], inNormal;
DP3 xfNormal.y, mvinv[1], inNormal;
DP3 xfNormal.z, mvinv[2], inNormal;
MOV xfNormal.z, -xfNormal.z;
# normalizamos...
DP3 xfNormal.w, xfNormal, xfNormal;
RSQ xfNormal.w, xfNormal.w;
MUL xfNormal, xfNormal, xfNormal.w;

# Ahora viene la gracia. Dados dos vectores normalizados, el coseno de su angulo es su producto
# escalar. Aún mejor, como el vector de vista es (0,0,1), el coseno es simplemente la componente
# Z del vector normal. Para rematar, el valor absoluto del coseno va entre 0 y 1: 1 para una 
# normal paralela al vector de vista (perpendicular a la pantalla) y 0 para una perpendicular
# (paralela a la pantalla). Como queremos que el degradado dependa de ese angulo, ponemos el 
# alpha según el valor absoluto de la Z de la normal :).

ABS xfNormal.z, xfNormal.z;
LOG inPosTemp, xfNormal.z;
MUL inPosTemp, inPosTemp, 0.4;
#SUB outColor.w, 1.0, xfNormal.z;
ADD outColor.w, xfNormal.z, inPosTemp.z;
#ABS outColor.w, xfNormal.z;	# Sencillo a la par que elegante, verdad?
MOV outColor.xyz, inColor;
END